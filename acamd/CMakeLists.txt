# ----------------------------------------------------------------------------
# @file    acamd/CMakeLists.txt
# @brief   input to the CMake build system for building acam daemon
# @author  David Hale <dhale@caltech.edu>
# ----------------------------------------------------------------------------

cmake_minimum_required( VERSION 3.12 )

set( ACAMD_DIR ${PROJECT_BASE_DIR}/acamd )

set( CMAKE_CXX_STANDARD 17 )

# C-Python API Development libraries and headers
#
find_package( Python3 COMPONENTS Development )
if ( Python3_FOUND )
  list( GET Python3_INCLUDE_DIRS 0 PYTHON_DEV )
  list( GET Python3_LIBRARIES 0 PYTHON_LIB )
else()
  find_path( PYTHON_DEV "Python.h" PATHS /usr/include/python3.9 )
  find_library( PYTHON_LIB NAMES python3.9 libpython3.9 PATHS /usr/lib64 )
endif()
if ( UNIX AND NOT APPLE )
  add_link_options( -lcrypt -lpthread -ldl -lutil -lm -lm )
endif()

# ZeroMQ
#
find_library( ZMQPP_LIB NAMES zmqpp libzmqpp
              PATHS ${NGPS_ZMQ_SEARCH_PATHS} ${NGPS_ZMQ_FIND_MODE} )
find_library( ZMQ_LIB NAMES zmq libzmq
              PATHS ${NGPS_ZMQ_SEARCH_PATHS} ${NGPS_ZMQ_FIND_MODE} )

# CFITSIO, CCfits
#
find_library( CCFITS_LIB CCfits NAMES libCCfits PATHS /usr/local/lib /opt/homebrew/lib )
find_library( CFITS_LIB cfitsio NAMES libcfitsio  PATHS /usr/local/lib /opt/homebrew/lib )

include_directories( ${PROJECT_BASE_DIR}/utils )
include_directories( ${PROJECT_BASE_DIR}/PI )
include_directories( ${PROJECT_BASE_DIR}/Andor )
include_directories( ${PROJECT_BASE_DIR}/emulator )
include_directories( ${PROJECT_BASE_DIR}/common )
include_directories( ${PROJECT_BASE_DIR}/tcsd )
include_directories( ${PYTHON_DEV} )

# -- MySQL database ------------------------------------------------------------
#
set ( MYSQL_DIR "/usr/local/mysql/connector" )
find_path( MYSQL_API "mysqlx/xdevapi.h"
           PATHS ${MYSQL_DIR} /usr/local/opt/mysql-connector-c++ /opt/homebrew/opt/mysql-connector-c++
           PATH_SUFFIXES include )
set( MYSQL_INCLUDES "${MYSQL_API}" )
find_library( MYSQL_LIB NAMES mysqlcppconnx mysqlcppconn8 mysqlcppconn
              PATHS ${MYSQL_DIR} /usr/local/opt/mysql-connector-c++ /opt/homebrew/opt/mysql-connector-c++
              PATH_SUFFIXES lib lib64 )
include_directories( ${MYSQL_INCLUDES} )

set( EMULATOR_DIR ${PROJECT_BASE_DIR}/emulator )
add_library( andorsim STATIC
        ${EMULATOR_DIR}/andorsim.cpp
        ${EMULATOR_DIR}/atmcdLXdsim.cpp
        )
target_link_libraries( andorsim
        )

add_executable(acamd 
        ${ACAMD_DIR}/acamd.cpp 
        ${ACAMD_DIR}/acam_server.cpp 
        ${ACAMD_DIR}/acam_interface.cpp 
        ${ACAMD_DIR}/acam_fits.cpp
        ${ACAMD_DIR}/motion_interface.cpp 
        ${PYTHON_DEV}
        ${MYSQL_INCLUDES}
        )

target_link_libraries(acamd
        andor
        physik_instrumente
        network
        common
        tcsd_client
        logentry
        utilities
        skyinfo
        database
        ${ZMQPP_LIB}
        ${ZMQ_LIB}
        ${PYTHON_LIB}
        ${CCFITS_LIB}
        ${CFITS_LIB}
        ${CMAKE_THREAD_LIBS_INIT}
        ${MYSQL_LIB}
        )

target_compile_options(acamd PRIVATE -g -Wall -O1 -Wno-variadic-macros -ggdb)

add_executable( newpytest
        ${ACAMD_DIR}/newpytest.cpp 
        ${PYTHON_DEV}
        )

target_link_libraries( newpytest
        ${PYTHON_LIB}
        )

add_executable( pytest
        ${ACAMD_DIR}/pytest.cpp 
        ${PYTHON_DEV}
        )

target_link_libraries( pytest
        ${PYTHON_LIB}
        )

# -- External Definitions ------------------------------------------------------
#
# The following external definitions keep the definitions in one place
# rather than in each of the config files. These include files are used to
# generate the .cfg files.
#
INCLUDE( ${PROJECT_BASE_DIR}/Config/portdefs )    # portdefs is the master list of ports
INCLUDE( ${PROJECT_BASE_DIR}/Config/ets8p )       # ports in the ETS8P terminal server
INCLUDE( ${PROJECT_BASE_DIR}/Config/privatelan )  # private IP addresses
set ( ACAMD_CFG_COMMENT "This file is automatically generated. Manual edits may be overwritten." )
CONFIGURE_FILE( ${PROJECT_BASE_DIR}/Config/acamd.cfg.in ${PROJECT_BASE_DIR}/Config/acamd.cfg )
